# -*- python -*-

from __future__ import absolute_import, division, print_function
import glob
import os
import os.path

Import('env testruns')

# Bump this if you break binary compatibility (e.g. renumber backends)
hammer_shlib_version = "1.0.0"

dist_headers = [
    'hammer.h',
    'allocator.h',
    'compiler_specifics.h',
    'glue.h',
    'internal.h',
    'platform.h'
]

parsers_headers = [
    'parsers/parser_internal.h'
]

backends_headers = [
    'backends/missing.h',
    'backends/params.h'
]

parsers = ['parsers/%s.c'%s for s in
           ['action',
            'and',
            'attr_bool',
            'bind',
            'bits',
            'bytes',
            'butnot',
            'ch',
            'charset',
            'choice',
            'difference',
            'end',
            'endianness',
            'epsilon',
            'ignore',
            'ignoreseq',
            'indirect',
            'int_range',
            'many',
            'not',
            'nothing',
            'optional',
            'permutation',
            'sequence',
            'token',
            'unimplemented',
            'whitespace',
            'xor',
            'value',
            'seek']]

backends = ['backends/%s.c' % s for s in
            ['missing', 'packrat', 'params']]

misc_hammer_parts = [
    'allocator.c',
    'benchmark.c',
    'bitreader.c',
    'bitwriter.c',
    'cfgrammar.c',
    'datastructures.c',
    'desugar.c',
    'glue.c',
    'hammer.c',
    'pprint.c',
    'registry.c',
    'system_allocator.c',
    'sloballoc.c']

misc_hammer_parts += ['platform_bsdlike.c']

# Use repository root to avoid variant_dir path leakage
project_root = Dir('#').abspath
tests_dir = os.path.join(project_root, 'tests')
src_dir = os.path.join(project_root, 'src')

ctests = ['t_benchmark.c',
          't_bitreader.c',
          't_bitwriter.c',
          't_parser.c',
          't_grammar.c',
          't_misc.c',
          't_mm.c',
          't_names.c',
          't_regression.c']

# Auto-discover test files matching pattern test_*.c
combinator_tests = sorted(glob.glob(os.path.join(tests_dir, 'test_*.c')))
# Filter out test_suite.c if it exists
combinator_tests = [os.path.basename(t) for t in combinator_tests if 'test_suite.c' not in t]
ctests.extend(combinator_tests)


static_library_name = 'hammer'
build_shared_library=True

if 'GPROF' in env and env['GPROF'] == 1:
    # Disable the shared library (it won't work with gprof) and rename the static one
    build_shared_library=False
    static_library_name = 'hammer_pg'

# Markers for later
libhammer_static = None
libhammer_shared = None

if build_shared_library:
    libhammer_shared = env.SharedLibrary('hammer', parsers + backends + misc_hammer_parts, \
                                     SHLIBVERSION=hammer_shlib_version)
libhammer_static = env.StaticLibrary(static_library_name, parsers + backends + misc_hammer_parts)

if libhammer_shared is not None:
    Default(libhammer_shared, libhammer_static)
    env.Install('$libpath', [libhammer_static, libhammer_shared])
else:
    Default(libhammer_static)
    env.Install('$libpath', [libhammer_static])

env.Append(CPPPATH=[Dir('#').abspath, src_dir])
env.Install('$incpath', dist_headers)
env.Install('$parsersincpath', parsers_headers)
env.Install('$backendsincpath', backends_headers)
env.Install('$pkgconfigpath', '../../../libhammer.pc')

if GetOption('with_tests'):
    testenv = env.Clone()
    testenv.ParseConfig('pkg-config --cflags --libs glib-2.0')
    # Add src/ to include path so tests can include hammer.h, internal.h, etc.
    testenv.Prepend(CPPPATH=[src_dir, os.path.join(src_dir, 'parsers')])
    if libhammer_shared is not None:
        testenv.Append(LIBS=['hammer'])
    else:
        testenv.Append(LIBS=[static_library_name])
    testenv.Prepend(LIBPATH=['.'])
    # Build test files from tests/ directory
    test_sources = [os.path.join(tests_dir, t) for t in ctests] + [os.path.join(tests_dir, 'test_suite.c')]
    ctestexec = testenv.Program(os.path.join('$BUILD_BASE', 'test_suite'), test_sources, 
                                LINKFLAGS='--coverage' if testenv.GetOption('coverage') else None)
    ctest = Alias('testc', [ctestexec], ''.join(['env LD_LIBRARY_PATH=', os.path.dirname(ctestexec[0].path), ' ', ctestexec[0].path]))
    AlwaysBuild(ctest)
    testruns.append(ctest)

if libhammer_shared is not None:
    Export('libhammer_static libhammer_shared')
else:
    Export('libhammer_static')
